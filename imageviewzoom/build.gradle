apply plugin: 'android-library'

group 'it.sephiroth.android.library.imageviewzoom'
version '1.0.0'

android {
    compileSdkVersion 19
    buildToolsVersion "18.1.1"

    defaultConfig {
        versionCode 1
        versionName version
        minSdkVersion 9
        targetSdkVersion 19
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

task displayInfo() {
    doLast {
        logger.lifecycle "  project name: ${project.name} (root: ${rootProject.name})"
        logger.lifecycle "  project version: ${project.version}"
        logger.lifecycle "  project group: ${project.group}"
    }
}

project.ext.outJar = project.name + '-' + version + '.jar'

task clearJar(type: Delete) {
    delete 'build/libs/' + project.ext.outJar
}

task makeJar(type: Copy) {
    from('build/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename ('classes.jar', project.ext.outJar)
}


def generatePom() {
    def pom = pom{
        project {
            groupId project.group
            artifactId project.name
            version project.version
            description 'Android ImageView widget with zoom and pan capabilities'
            inceptionYear '2012'
            url 'https://github.com/sephiroth74/ImageViewZoom'

            licenses {
                license {
                    name 'MIT license'
                    url 'http://opensource.org/licenses/mit-license.php'
                    distribution 'repo'
                }
            }

            scm {
                url                 "https://github.com/sephiroth74/ImageViewZoom"
                connection          "scm:git:git@github.com:sephiroth74/ImageViewZoom.git"
                developerConnection "scm:git:git@github.com:sephiroth74/ImageViewZoom.git"
            }


            developers {
                developer {
                    id 'sephiroth74'
                    name 'Alessandro Crugnola'
                    email 'alessandro.crugnola@gmail.com'
                    url 'http://blog.sephiroth.it'
                    roles {
                        role 'author'
                        role 'developer'
                    }
                }
            }

            repositories {
                repository {
                    id 's3.release'
                    url 'http://maven.aviary.com/repo/release'
                }
            }
        }
    }
}

task writePom {
    ext.pomXml = file("pom.xml")
    inputs.file(project.buildFile)
    outputs.file(pomXml)
    doLast {
        generatePom().writeTo(pomXml)
    }
}

uploadArchives {
    doFirst {
        repositories.mavenDeployer.pom = generatePom()
    }

    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars

            repository(url: mavenUploadServer + mavenRepo + mavenReleases){
                authentication(userName: mavenUsername, password: mavenPassword)
            }
        }
    }
}


task updateVersion {
    def source = new File("src/it/sephiroth/android/library/imagezoom/ImageViewTouchBase.java")

    onlyIf { task ->
        
        def src = project.file(source)
        def text = src.text

        def pattern = /(?ms)^\s*public static final String VERSION = "($project.version)";$/
        def matcher = ( text =~ pattern )

        if( matcher.size() > 0 ) {
            return false
        }

        return true
    }

    doLast {
        def src = project.file(source)

        println "Version mismatch. updating version to ${project.version}"

        def text = src.text
        def pattern = /(?ms)^(\s*)(public static final String VERSION = ")([0-9\.]+)(";)$/
        def matcher = ( text =~ pattern )

        if( matcher.size() > 0 ) {
            text = matcher.replaceFirst("\$1\$2" + project.version + "\$4")
            src.setText(text)
            println "Updated version of AviaryCds.java to ${project.version}"
        }

    }
}

clean.dependsOn(clearJar)
uploadArchives.dependsOn(build)
build.dependsOn(writePom, makeJar)
preBuild.dependsOn(updateVersion)